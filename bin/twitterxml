#!/usr/bin/perl

use Net::Twitter::Lite::WithAPIv1_1;
use Scalar::Util 'blessed';
use Mojo::ByteStream 'b';
use XML::Simple;
use Data::Rmap qw(rmap_ref);
use Try::Tiny;
use Path::Tiny;
use YAML::Tiny;
use autodie;

my $config_file = path('/etc/twitterxml.conf');

my $config = YAML::Tiny->read($config_file)
  or die "Can't parse $config_file: $YAML::Tiny::errstr\n";

# we just need the first document
$config = $config->[0];

my $output_dir = path($config->{output_dir});
die "Missing output directory in config file\n"
	unless $output_dir;
$output_dir->mkpath();

my $nt = Net::Twitter::Lite::WithAPIv1_1->new(
      consumer_key        => $config->{consumer_key},
      consumer_secret     => $config->{consumer_secret},
      access_token        => $config->{token},
      access_token_secret => $config->{token_secret},
);

for my $user ( @{$config->{user}} ) {
   try {
      my $statuses = $nt->user_timeline({screen_name => $user});
      rmap_ref { $_[0]{seen} = {}; $_ = "$_" if JSON::is_bool($_) } $statuses;
      my $xml = XMLout({ status => $statuses }, NoAttr => 1);
      $output_dir->child("$user.xml")->touch->spew(b($xml)->encode);
  }
  catch {
      my $error = blessed $_ && $_->isa('Net::Twitter::Lite::Error') ? $_->error : $_;
      warn $error;
  };
}
